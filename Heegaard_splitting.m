// In this script we implement the computation of the first homology
// group of a CW complex described in Section 7.2 of the article 
//'On the cohomology groups of real Lagrangians and Hodge numbers'
//by Hülya Argüz and Thomas Prince.
//
// The matrix 'Disk_attaching' describes the attaching map
// of 120 discs to a union of 84 line segments.
//
// The line segments are the edges of the preimage of a graph \Gamma
// under the 7-to-1 cover \breve{\pi}.
//
// The vertices of \Gamma are the vertex with label 5 and a point
// in the interior of tetrahedron 5. The edges of \Gamma are in
// bijection with pairs (i,j), where:
// 1) i in {1,2,3,4} indexes a tetrahedron.
// 2) j in {1,2,3} indexes a vertex of the i-th tetrahedron.
//
// Note that the jth vertex of the ith tetrahedron has label equal to vertices_of_ts[i][j].
// As the entries of vertices_of_ts[i] are in ascending order, the value 
// of vertices_of_ts[i][j] is less than or equal to 4.
//
// The 84 rows of Disk_attaching are indexed by triples (i,j,k), where 
// 1) i and j determine an edge of \Gamma, as above.
// 2) k in {1,...,7} indexes a component of the preimage of the corresponding edge of \Gamma.

Disk_attaching := Matrix(Z,84,120,[]);

// Each vertex of the graph \Gamma is a point in an integral affine manifold.
// We enumerate the fibres 1,...,7. Each edge of \Gamma connects
// the two vertices, and hence the preimage of each edge induces
// a permutation of the set {1,...,7}. These permutations are stored
// in the associative array 'Matchings'.
//
// 'Matchings' stores the permutation obtained by composing the following permuations from 'Paste'.
// 1) The permutation induced by moving from vertex 5 to tetrahedron i.
// 2) The permutation induced by moving from tetrahedron i to vertex vertices_of_ts[i][j]
// 3) The permutation induced by moving from vertex vertices_of_ts[i][j] to tetrahedron 5.
// See 'setup_cover.m' for further details on the associative array 'Paste'.

Matchings := AssociativeArray();

for i in [1..4] do
	for j in [1..3] do
		Matchings[[i,j]] := Paste[[5,vertices_of_ts[i][j]]]^-1*Paste[[i,vertices_of_ts[i][j]]]*Paste[[i,5]]^-1;
	end for;
end for;

// Columns of 'Disk_attaching' correspond to disks. These disks in bijection with the orbits of
// the action of group generated by Matchings[[i,k]]^-1*Matchings[[i,j]] on the set {1,...,7}.
// There are 5 such orbits for any choice of i, j, and k. Hence the columns of the matrix 
// Disk_attaching are grouped into 24 blocks of size five. These 24 blocks correspond to segments 
// of the discriminant locus \tilde{\Delta}.
//
// We first consider the 12 segments of the discriminant locus contained in 
// faces of tetrahedron 5. These 12 segments are indexed by the list 'pairs'.

pairs := [[i,j] : j in [1..3], i in [1..4]];
for ind in pairs do

	block_index := Index(pairs,ind);

	pair := Remove([1,2,3],ind[2]);
	orbits := Orbits( sub <S7 | Matchings[[ind[1],pair[2]]]^-1*Matchings[[ind[1],pair[1]]] > );
	for k in [1..5] do
		column_index := 5*(block_index-1)+k;
			for l in orbits[k] do
				row_blocks := [Index(pairs,[ind[1],pair[m]]) : m in [1,2]];
				row_indices := [7*(row_blocks[m]-1)+l: m in [1,2]];

				Disk_attaching[row_indices[1], column_index] := 1;
				Disk_attaching[row_indices[2], column_index] := -1;
			end for;
	end for;
end for;

// The remaining 12 segments of \tilde{\Delta} are disjoint from tetrahedron with index 5.
// Fixing such a segment e, we pick a disk D_e in the handlebody W_1 which intersects e in a single point.
// We then compute a homotopy of the boundary of D_e in W_2 to a union of the edges of \Gamma.
//
// Recalling that edges of \Gamma are indexed by pairs (i,j), for i in {1,2,3,4}, j in {1,2,3},
// 'second_pairs' is a list of pairs of edges (E_1, E_2) of \Gamma such that the
// boundary of D_e is homotopic to the union of E_1 and E_2 in the handlebody W_2
// for each of the 12 edges e of \tilde{\Delta} disjoint from tetrahedron 5.

second_pairs := [
	[[3,1],[4,1]],	[[3,2],[4,2]],	[[3,2],[1,1]],	[[3,3],[1,3]],
	[[2,2],[1,2]],	[[2,3],[1,3]],	[[2,1],[4,1]],	[[2,2],[4,3]],
	[[2,1],[3,1]],	[[2,3],[3,3]],	[[1,1],[4,2]],	[[1,2],[4,3]]
	];

for ind in second_pairs do
	
	block_index := Index(second_pairs,ind);

	orbits := Orbits( sub <S7 | Matchings[ind[2]]^-1*Matchings[ind[1]] > );
	for k in [1..5] do
		column_index := 5*(block_index-1)+k;
			for l in orbits[k] do
				row_block := [Index(pairs,ind[m]) : m in [1,2]];
				row_indices := [7*(row_block[m]-1)+l: m in [1,2]];

				Disk_attaching[row_indices[1], column_index+60] := 1;
				Disk_attaching[row_indices[2], column_index+60] := -1;
			end for;
	end for;
end for;

// We remove 13 rows corresponding to a spanning tree of the preimage of \Gamma.
// Note that this step is unnecessary, we can simply observe that the matrix
// Disk_attaching has rank 71, and compute its Smith Normal Form directly.

RemoveRow(~Disk_attaching,54);
for i in [1..7] do
	RemoveRow(~Disk_attaching,50-i);	
end for;
RemoveRow(~Disk_attaching,22);

for i in [1,2,4,5] do
	RemoveRow(~Disk_attaching,15-i);	
end for;

// We compute the Smith Normal Form of the matrix Disk_attaching to determine
// the first homology group of the corresponding CW complex.
[SmithForm(Disk_attaching)[i,i] : i in [1..71]];
